#!/usr/bin/awk -f

# killrenegade statistics   version 1.2   august 2008   written by Feherke 
# create HTML statistics from the plain text log file


function htmlhead(file,title)
{
  print \
  "<!DOCTYPE HTML PUBLIC \"-//W3C//DTD HTML 4.01//EN\">\n" \
  "<html lang=\"en\">\n" \
  "<head>\n" \
  "<title>Kill Renegade Statistics - " title "</title>\n" \
  "<link rel=\"stylesheet\" type=\"text/css\" href=\"killrenegade.css\">\n" \
  "</head>\n" \
  "<body>\n" \
  "<h1>" title "</h1>\n" \
  "<hr>" \
  > file
}

function htmlfoot(file,     t)
{
  printf "<p><a href=\"index.htm\">Main page</a> | <a href=\"allkill.htm\">All kills</a> | " > file
  for (t in column) printf "<a href=\"all" t ".htm\">All " tolower(word[t,"name"]) "s</a> | " > file
  print \
  "<a href=\"datetime.htm\">Date &amp; time</a> | <a href=\"about.htm\">About</a></p>\n" \
  "<address>generated by Kill Renegade &nbsp; version 1.2 &nbsp; august 2008 &nbsp; written by Feherke</address>\n" \
  "</body>\n" \
  "</html>\n" \
  > file
  close(file)
}

function tablehead(file,title,head,     tmp,nr,i)
{
  print \
  "<h2>" title "</h2>\n" \
  "<table>" \
  > file
  printf "<tr>" > file
  nr=split(head,tmp)
  for (i=1;i<=nr;i++) printf "<th>" tmp[i] "</th>" > file
  print "</tr>" > file
}

function tablefoot(file,more,     tmp,tmp2,nr,i)
{
  print "</table>" > file
  if (more) {
    printf "<p>" > file
    nr=split(more,tmp,"\n")
    for (i=1;i<=nr;i++) {
      split(tmp[i],tmp2,"\t")
      if (!tmp2[2]) tmp2[2]="See more"
      printf "<a href=\"" tmp2[1] "\">" tmp2[2] "</a>" (i<nr?" | ":"") > file
    }
    print "</p>" > file
  }
  print "<hr>" > file
}

function aindex(array,data,type,ind,     lst,tmp,nr,max,key,i,j)
{
  if (ind[0]) delete ind
  nr=0; for (i in array) nr++

  nr=split(data[type],lst)
  for (i=0;i<nr;i++) {
    max=0
    for (j=1;j<=nr;j++) if (!(lst[j] in tmp) && max<array[type,lst[j]]) { key=lst[j]; max=array[type,lst[j]] }
    ind[i]=key; tmp[key]
  }

  return nr
}


NR==1 {  logfile=FILENAME }


# |3 [- (; | |\|

BEGIN {
  start=systime()
  perpage=100
  lasttop=10
  if (match(htmldir,"[^/]$")) htmldir=htmldir "/"

  column["user"]=1;  column["sign"]=7; column["comm"]=6
  word["comm","name"]="Command"; word["comm","colt"]="User"; word["comm","coli"]="user"; word["comm","coln"]=1
  word["user","name"]="User"; word["user","colt"]="Command"; word["user","coli"]="comm"; word["user","coln"]=6
  word["sign","name"]="Signal"; word["sign","colt"]="Command"; word["sign","coli"]="comm"; word["sign","coln"]=6
}


# |\/| /\ | |\|

{
  if (!(nr["kill"]%perpage)) {
    if (file["kill"]) {
      tablefoot(htmldir "kill_" file["kill"] ".htm",(file["kill"]>1?"kill_" file["kill"]-1 ".htm\tPrevious " perpage " ( " file["kill"]-1 " )\n":"") ("kill_" file["kill"]+1 ".htm\tNext " perpage " ( " file["kill"]+1 " )"))
      htmlfoot(htmldir "kill_" file["kill"] ".htm")
    }
    file["kill"]++
    htmlhead(htmldir "kill_" file["kill"] ".htm","Kill")
    tablehead(htmldir "kill_" file["kill"] ".htm","All kills, part " file["kill"],"User Date CPU Memory Command Signal")
  }
  nr["kill"]++
  print "<tr><td><a href=\"user" $1 ".htm\">" $1 "</a></td><td>" $2,$3 "</td><td class=\"nr\">" $4 "</td><td class=\"nr\">" $5 "</td><td><a href=\"comm" $6 ".htm\">" $6 "</a></td><td><a href=\"sign" $7 ".htm\">" $7 "</a></td></tr>" > htmldir "kill_" file["kill"] ".htm"

  for (t in column) {
    v=$column[t]
    if (!(nr[t,v]%perpage)) {
      if (file[t,v]) {
        tablefoot(htmldir t v "_" file[t,v] ".htm",(file[t,v]>1?t v "_" file[t,v]-1 ".htm\tPrevious " perpage " ( " file[t,v]-1 " )\n":"") (t v "_" file[t,v]+1 ".htm\tNext " perpage " ( " file[t,v]+1 " )"))
	htmlfoot(htmldir t v "_" file[t,v] ".htm")
      }
      file[t,v]++
      htmlhead(htmldir t v "_" file[t,v] ".htm",word[t,"name"] " " v)
      tablehead(htmldir t v "_" file[t,v] ".htm","All kills of " tolower(word[t,"name"]) " " v ", part " file[t,v],word[t,"colt"] "s Date CPU Memory")
    }
    nr[t,v]++; if (nr[t,v]==1) data[t]=data[t] " " v
    sum["cpu",t,v]+=$4; sum["mem",t,v]+=$5
    print "<tr><td><a href=\"" word[t,"coli"] $word[t,"coln"] ".htm\">" $word[t,"coln"] "</a></td><td>" $2,$3 "</td><td class=\"nr\">" $4 "</td><td class=\"nr\">" $5 "</td></tr>" > htmldir t v "_" file[t,v] ".htm"
  }

  nrkill[$1,$6,$7]++

  split(strftime("%Y %m %d %H %w",mktime(gensub("[-:]"," ","g",$2 " " $3))),tmp)
  for (i=1;i<=5;i++) date[i,strtonum(tmp[i])]++

  lasti=(lasti+1)%lasttop  
  last[lasti]=$0
}


# [- |\| |)

END {
  tablefoot(htmldir "kill_" file["kill"] ".htm",(file["kill"]>1?"kill_" file["kill"]-1 ".htm\tPrevious " perpage " ( " file["kill"]-1 " )":""))
  htmlfoot(htmldir "kill_" file["kill"] ".htm")

  htmlhead(htmldir "allkill.htm","All kills")
  tablehead(htmldir "allkill.htm","All kills categorised","User Command Signal Count")
  n1=split(data["user"],tmp1); n2=split(data["comm"],tmp2); n3=split(data["sign"],tmp3)
  for (i1=1;i1<=n1;i1++) for (i2=1;i2<=n2;i2++) for (i3=1;i3<=n3;i3++)
  print "<tr><td><a href=\"user" tmp1[v1] ".htm\">" tmp1[i1] "</a></td><td><a href=\"comm" tmp2[i2] ".htm\">" tmp2[i2] "</a></td><td><a href=\"sign" tmp3[i3] ".htm\">" tmp3[i3] "</td><td class=\"nr\">" nrkill[tmp1[i1],tmp2[i2],tmp3[i3]] "</a></td></tr>" > htmldir "allkill.htm"
  tablefoot(htmldir "allkill.htm",("kill_1.htm\tFirst " perpage " ( 1 )\nkill_" file["kill"] ".htm\tLast " perpage " ( " file["kill"] " )"))
  htmlfoot(htmldir "allkill.htm")

  htmlhead(htmldir "index.htm","Main page")

  tablehead(htmldir "index.htm","Last " lasttop " kills","# User Date CPU Memory Command Signal")
  for (i=0;i<lasttop;i++) {
    split(last[(lasti+lasttop-i)%lasttop],tmp)
    print "<tr><td class=\"nr\">" i+1 "</td><td><a href=\"user" tmp[1] ".htm\">" tmp[1] "</a></td><td>" tmp[2],tmp[3] "</td><td class=\"nr\">" tmp[4] "</td><td class=\"nr\">" tmp[5] "</td><td><a href=\"comm" tmp[6] ".htm\">" tmp[6] "</a></td><td><a href=\"sign" tmp[7] ".htm\">" tmp[7] "</a></td></tr>" > htmldir "index.htm"
  }
  tablefoot(htmldir "index.htm","allkill.htm")

  for (t in column) {
    split(data[t],tmp)
    for (n in tmp) {
      v=tmp[n]
      tablefoot(htmldir t v "_" file[t,v] ".htm",(file[t,v]>1?t v "_" file[t,v]-1 ".htm\tPrevious " perpage " ( " file[t,v]-1 " )":""))
      htmlfoot(htmldir t v "_" file[t,v] ".htm")

      htmlhead(htmldir t v ".htm",word[t,"name"] " " v)
      tablehead(htmldir t v ".htm","Kills brief","Count CPU Memory")
      print "<tr><td class=\"nr\">" nr[t,v] "</td><td class=\"nr\">" sprintf("%.2f",sum["cpu",t,v]/nr[t,v]) "</td><td class=\"nr\">" sprintf("%.2f",sum["mem",t,v]/nr[t,v]) "</td></tr>" > htmldir t v ".htm"
      tablefoot(htmldir t v ".htm",(t v "_1.htm\tFirst " perpage " ( 1 )\n" t v "_" file[t,v] ".htm\tLast " perpage " ( " file[t,v] " )"))
      htmlfoot(htmldir t v ".htm")
    }

    htmlhead(htmldir "all" t ".htm","All " tolower(word[t,"name"]) "s")
    n=aindex(nr,data,t,ind)
    tablehead(htmldir "index.htm","Top " lasttop " " tolower(word[t,"name"]) "s","# " word[t,"name"] " Count CPU Memory")
    for (i=0;i<(n<lasttop?n:lasttop);i++) print "<tr><td class=\"nr\">" i+1 "</td><td><a href=\"" t ind[i] ".htm\">" ind[i] "</a></td><td class=\"nr\">" nr[t,ind[i]] "</td><td class=\"nr\">" sprintf("%.2f",sum["cpu",t,ind[i]]/nr[t,ind[i]]) "</td><td class=\"nr\">" sprintf("%.2f",sum["mem",t,ind[i]]/nr[t,ind[i]]) "</td></tr>" > htmldir "index.htm"
    tablefoot(htmldir "index.htm","all" t ".htm")
    tablehead(htmldir "all" t ".htm","All killed " tolower(word[t,"name"]) "s","# " word[t,"name"] " Count CPU Memory")
    for (i=0;i<n;i++) print "<tr><td class=\"nr\">" i+1 "</td><td><a href=\"" t ind[i] ".htm\">" ind[i] "</a></td><td class=\"nr\">" nr[t,ind[i]] "</td><td class=\"nr\">" sprintf("%.2f",sum["cpu",t,ind[i]]/nr[t,ind[i]]) "</td><td class=\"nr\">" sprintf("%.2f",sum["mem",t,ind[i]]/nr[t,ind[i]]) "</td></tr>" > htmldir "all" t ".htm"
    tablefoot(htmldir "all" t ".htm")
    htmlfoot(htmldir "all" t ".htm")
  }

  htmlfoot(htmldir "index.htm")

  htmlhead(htmldir "datetime.htm","Date &amp; time counts")
  n=split("Year Month Day Weekday Hour",tmp); split("10000 1 1 0 0",min); split("0 12 31 6 23",max)
  for (v in date) if (match(v,"1" SUBSEP "(.*)",tmp2)) { tmp2[1]=tmp2[1]+0; min[1]=tmp2[1]<min[1]?tmp2[1]:min[1]; max[1]=tmp2[1]>max[1]?tmp2[1]:max[1] }
  for (i=1;i<=n;i++) {
    tablehead(htmldir "datetime.htm","By " tolower(tmp[i]) "s",tmp[i] " Count")
    for (v=min[i];v<=max[i];v++) print "<tr><td>" v "</td><td class=\"nr\">" date[i,v] "</td></tr>" > htmldir "datetime.htm"
    tablefoot(htmldir "datetime.htm")
  }
  htmlfoot(htmldir "datetime.htm")

  if (system("test -f \"" htmldir "killrenegade.css\"")) print \
  "h1,h2,h3 { margin: 0; }\n" \
  "table,th { border-style: solid; border-width: 1px; border-color: gray; }\n" \
  "th { text-align: left; background-color: silver; }\n" \
  "td.nr { text-align: right; }\n" \
  "p {  margin: 0; }\n" \
  "address { font-size: small; }\n" \
  "hr { height: 1px; background-color: silver; border-style: none; }" \
  > htmldir "killrenegade.css"

  htmlhead(htmldir "about.htm","About");
  tablehead(htmldir "about.htm","Generated by","Name Version Author")
  print "<tr><td>Kill Renegade</td><td class=\"nr\">1.2</td><td>Feherke</td></tr>" > htmldir "about.htm"
  tablefoot(htmldir "about.htm")
  tablehead(htmldir "about.htm","Generated on","Host User Date")
  print "<tr><td>" ENVIRON["HOSTNAME"] "</td><td>" ENVIRON["USER"] "</td><td>" strftime("%Y-%m-%d %H:%M:%S",systime()) "</td></tr>" > htmldir "about.htm"
  tablefoot(htmldir "about.htm")
  tablehead(htmldir "about.htm","Generated from","File Count Time")
  print "<tr><td>" logfile "</td><td class=\"nr\">" NR "</td><td>" systime()-start " sec </td></tr>" > htmldir "about.htm"
  tablefoot(htmldir "about.htm")
  htmlfoot(htmldir "about.htm");
}
